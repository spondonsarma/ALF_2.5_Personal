!  Copyright (C) 2016 - 2020 The ALF project
!
!     The ALF project is free software: you can redistribute it and/or modify
!     it under the terms of the GNU General Public License as published by
!     the Free Software Foundation, either version 3 of the License, or
!     (at your option) any later version.
!
!     The ALF project is distributed in the hope that it will be useful,
!     but WITHOUT ANY WARRANTY; without even the implied warranty of
!     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!     GNU General Public License for more details.
!
!     You should have received a copy of the GNU General Public License
!     along with ALF.  If not, see http://www.gnu.org/licenses/.
!
!     Under Section 7 of GPL version 3 we require you to fulfill the following additional terms:
!
!     - It is our hope that this program makes a contribution to the scientific community. Being
!       part of that community we feel that it is reasonable to require you to give an attribution
!       back to the original authors if you have benefitted from this program.
!       Guidelines for a proper citation can be found on the project's homepage
!       http://alf.physik.uni-wuerzburg.de
!
!     - We require the preservation of the above copyright notice and this license in all original files.
!
!     - We prohibit the misrepresentation of the origin of the original source files. To obtain
!       the original source files please visit the homepage http://alf.physik.uni-wuerzburg.de .
!
!     - If you make substantial changes to the program we require you to either consider contributing
!       to the ALF project or to mark your material in a reasonable way as different from the original version


!--------------------------------------------------------------------
!> @author
!> ALF-project
!>
!> @brief
!> This module defines the  Hamiltonian and observables  for the Z2 lattice gauge model coupled to
!> fermionic and Z2 matter.
!--------------------------------------------------------------------


    Module Hamiltonian

      Use Operator_mod
      Use WaveFunction_mod
      Use Lattices_v3
      Use MyMats
      Use Random_Wrap
      Use Files_mod
      Use Matrix
      Use Observables
      Use Fields_mod
      Use Predefined_Hoppings
      use iso_fortran_env, only: output_unit, error_unit


      Implicit none

!>    Public variables. Have to be set by user
      Type (Operator), dimension(:,:), allocatable  :: Op_V
      Type (Operator), dimension(:,:), allocatable  :: Op_T
      Type (WaveFunction), dimension(:),   allocatable :: WF_L
      Type (WaveFunction), dimension(:),   allocatable :: WF_R
      Type (Fields)        :: nsigma
      Integer              :: Ndim
      Integer              :: N_FL
      Integer              :: N_SUN
      Integer              :: Ltrot
      Integer              :: Thtrot
      Logical              :: Projector
      Integer              :: Group_Comm
      Logical              :: Symm = .False. 


!>    Privat variables
      Type (Lattice),        private :: Latt
      Type (Unit_cell),      private :: Latt_unit
      Integer,               private :: L1, L2
      real (Kind=Kind(0.d0)),private :: ham_T, Ham_chem, Ham_g, Ham_J,  Ham_K, Ham_h,  Ham_TZ2, Ham_U
      real (Kind=Kind(0.d0)),private :: Dtau, Beta
      Character (len=64),    private :: Model, Lattice_type
      Logical,               private :: One_dimensional
      Integer, allocatable,  private :: List(:,:), Invlist(:,:)  ! For orbital structure of Unit cell
      real (Kind=Kind(0.d0)),private :: Zero = 1.D-10
      


      !>    Privat Observables
      Type (Obser_Vec ),  private, dimension(:), allocatable ::   Obs_scal
      Type (Obser_Latt),  private, dimension(:), allocatable ::   Obs_eq
      Type (Obser_Latt),  private, dimension(:), allocatable ::   Obs_tau

      !>    Storage for the Ising action
      Real (Kind=Kind(0.d0)), private :: DW_Ising_tau(-1:1), DW_Ising_Space(-1:1), DW_Ising_Flux(-1:1,-1:1)
      Real (Kind=Kind(0.d0)), private :: DW_Matter_tau(-1:1), DW_Ising_Matter(-1:1)
      Integer, allocatable  , private :: Field_list(:,:,:), Field_list_inv(:,:)


    contains


      Subroutine Ham_Set


#if defined (MPI) || defined(TEMPERING)
           use mpi
#endif
          Implicit none


          integer :: ierr
          Character (len=64) :: file1

          NAMELIST /VAR_lattice/  L1, L2, Lattice_type, Model


          NAMELIST /VAR_Z2_Matter/ ham_T, Ham_chem, Ham_g, Ham_J,  Ham_K, Ham_h, &
               &                   Dtau, Beta, ham_TZ2, Ham_U,  N_SUN 


#ifdef MPI
          Integer        :: Isize, Irank, igroup, irank_g, isize_g
          Integer        :: STATUS(MPI_STATUS_SIZE)
          CALL MPI_COMM_SIZE(MPI_COMM_WORLD,ISIZE,IERR)
          CALL MPI_COMM_RANK(MPI_COMM_WORLD,IRANK,IERR)
          call MPI_Comm_rank(Group_Comm, irank_g, ierr)
          call MPI_Comm_size(Group_Comm, isize_g, ierr)
          igroup           = irank/isize_g
          !if ( irank_g == 0 )   write(6,*) "Mpi Test", igroup, isize_g
#endif


#ifdef MPI
          If (Irank == 0 ) then
#endif
             OPEN(UNIT=5,FILE='parameters',STATUS='old',ACTION='read',IOSTAT=ierr)
             IF (ierr /= 0) THEN
                WRITE(error_unit,*) 'Ham_set: unable to open <parameters>',ierr
                error stop 1
             END IF
             READ(5,NML=VAR_lattice)
             CLOSE(5)
#ifdef MPI
          Endif
          CALL MPI_BCAST(L1          ,1  ,MPI_INTEGER,   0,MPI_COMM_WORLD,ierr)
          CALL MPI_BCAST(L2          ,1  ,MPI_INTEGER,   0,MPI_COMM_WORLD,ierr)
          CALL MPI_BCAST(Model       ,64 ,MPI_CHARACTER, 0,MPI_COMM_WORLD,IERR)
          CALL MPI_BCAST(Lattice_type,64 ,MPI_CHARACTER, 0,MPI_COMM_WORLD,IERR)
#endif
          Call Ham_latt

          if ( Model == "Z2_Matter" ) then
             N_FL = 1
             If ( Lattice_type  /= "Square" ) then
                Write(error_unit,*) "Ham_set: Z2_Matter is only implemented for a square lattice"
                error stop 1
             Endif
          else
             Write(error_unit,*) "Ham_set: Model not yet implemented!"
             error stop 1
          endif


#if defined(TEMPERING)
          write(File1,'(A,I0,A)') "Temp_",igroup,"/parameters"
          OPEN(UNIT=5,File=file1,STATUS='old',ACTION='read',IOSTAT=ierr)
          ham_T = 0.d0; Ham_chem = 0.d0; Ham_g = 0.d0; Ham_J = 0.d0
          Ham_K = 0.d0; Ham_h = 0.d0
          READ(5,NML=VAR_Z2_Matter)
          CLOSE(5)
#else
#if defined(MPI)
          If (Irank == 0 ) then
#endif
             OPEN(UNIT=5,FILE='parameters',STATUS='old',ACTION='read',IOSTAT=ierr)
             ham_T = 0.d0; Ham_chem = 0.d0; Ham_g = 0.d0; Ham_J = 0.d0
             Ham_K = 0.d0; Ham_h = 0.d0
             READ(5,NML=VAR_Z2_Matter)
             CLOSE(5)
             If (Abs(Ham_T) < Zero ) then
                Ham_J = 0.d0
                Ham_h = 0.d0
             endif
             If (Abs(Ham_TZ2) < Zero ) then
                Ham_J = 0.d0
                Ham_K = 0.d0
                Ham_g = 0.d0
             endif
#ifdef MPI
          endif
          CALL MPI_BCAST(ham_T    ,1,MPI_REAL8,0,Group_Comm,ierr)
          CALL MPI_BCAST(ham_TZ2  ,1,MPI_REAL8,0,Group_Comm,ierr)
          CALL MPI_BCAST(ham_chem ,1,MPI_REAL8,0,Group_Comm,ierr)
          CALL MPI_BCAST(ham_g    ,1,MPI_REAL8,0,Group_Comm,ierr)
          CALL MPI_BCAST(Ham_J    ,1,MPI_REAL8,0,Group_Comm,ierr)
          CALL MPI_BCAST(Ham_K    ,1,MPI_REAL8,0,Group_Comm,ierr)
          CALL MPI_BCAST(Ham_h    ,1,MPI_REAL8,0,Group_Comm,ierr)
          CALL MPI_BCAST(Dtau     ,1,MPI_REAL8,0,Group_Comm,ierr)
          CALL MPI_BCAST(Beta     ,1,MPI_REAL8,0,Group_Comm,ierr)
          CALL MPI_BCAST(Ham_U    ,1,MPI_REAL8,0,Group_Comm,ierr)
          CALL MPI_BCAST(N_SUN    ,1,MPI_INTEGER,0,Group_Comm,ierr)
#endif
#endif

           Call Ham_hop
           Ltrot = nint(beta/dtau)

           If  ( Model == "Z2_Matter" )  Call Setup_Ising_action_and_field_list 

#if defined(TEMPERING)
           write(File1,'(A,I0,A)') "Temp_",igroup,"/info"
#else
           File1 = "info"
#endif

#if defined(MPI) && !defined(TEMPERING)
           If (Irank == 0 ) then
#endif

              Open (Unit = 50,file=file1,status="unknown",position="append")
              Write(50,*) '====================================='
              Write(50,*) 'Model is      : ', Model
              Write(50,*) 'Lattice is    : ', Lattice_type
              Write(50,*) '# of orbitals : ', Ndim
              Write(50,*) 'Beta          : ', Beta
              Write(50,*) 'dtau,Ltrot    : ', dtau,Ltrot
              Write(50,*) 'N_SUN         : ', N_SUN
              Write(50,*) 'N_FL          : ', N_FL
              If (Abs(Ham_T) < Zero) then
                 Write(50,*) 't_Z2          : ', Ham_TZ2
                 Write(50,*) 'g_Z2          : ', Ham_g
                 Write(50,*) 'K_Gauge       : ', Ham_K
              elseif (Abs(Ham_TZ2) < Zero) then
                 Write(50,*) 't_fermion     : ', Ham_T
                 Write(50,*) 'h_Matter      : ', Ham_h
              else
                 Write(50,*) 't_Z2          : ', Ham_TZ2
                 Write(50,*) 'g_Z2          : ', Ham_g
                 Write(50,*) 'K_Gauge       : ', Ham_K
                 Write(50,*) 't_fermion     : ', Ham_T
                 Write(50,*) 'h_Matter      : ', Ham_h
                 Write(50,*) 'J_Gauge_Z2    : ', Ham_J
              endif
              Write(50,*) 'Ham_chem      : ', Ham_chem
              Write(50,*) 'Ham_U         : ', Ham_U
              close(50)
#if defined(MPI) && !defined(TEMPERING)
           endif
#endif
           call Ham_V

           
         end Subroutine Ham_Set

!--------------------------------------------------------------------
!> @author
!> ALF Collaboration
!>
!> @brief
!> Sets  the  Lattice
!--------------------------------------------------------------------
        Subroutine Ham_Latt

          Use Predefined_Lattices

          Implicit none
          ! Use predefined stuctures or set your own lattice.
          If ( L1 == 1 .or. L2 == 1 ) then
             Write(error_unit,*) 'Ham_Latt: One dimensional systems are not included '
             error stop 1
          endif
          Call Predefined_Latt(Lattice_type, L1,L2,Ndim, List,Invlist,Latt,Latt_Unit)

          
        end Subroutine Ham_Latt

!--------------------------------------------------------------------
!> @author
!> ALF Collaboration
!>
!> @brief
!> Sets  the Hopping
!--------------------------------------------------------------------
        Subroutine Ham_Hop

          Implicit none


          Type (Hopping_Matrix_type), Allocatable :: Hopping_Matrix(:)

          Real (Kind=Kind(0.d0) ), allocatable :: Ham_T_vec(:), Ham_Tperp_vec(:), Ham_Chem_vec(:), Phi_X_vec(:),&
               &                                  Phi_Y_vec(:),  Ham_T2_vec(:),  Ham_Lambda_vec(:)
          Integer, allocatable ::   N_Phi_vec(:)

          Logical ::  Bulk = .False.,  Checkerboard = .False.
          
          Allocate (Ham_T_vec(N_FL), Ham_T2_vec(N_FL), Ham_Tperp_vec(N_FL), Ham_Chem_vec(N_FL), &
               &    Phi_X_vec(N_FL), Phi_Y_vec(N_FL), N_Phi_vec(N_FL), Ham_Lambda_vec(N_FL) )

          ! Here we consider no N_FL  dependence of the hopping parameters.
          Ham_T_vec      = 0.d0
          Ham_Tperp_vec  = 0.d0
          Ham_Chem_vec   = Ham_Chem
          Phi_X_vec      = 0.d0
          Phi_Y_vec      = 0.d0
          Ham_T2_vec     = 0.d0
          Ham_Lambda_vec = 0.d0
          N_Phi_vec      = 0

          Call  Set_Default_hopping_parameters_square(Hopping_Matrix,Ham_T_vec, Ham_Chem_vec, Phi_X_vec, Phi_Y_vec, &
               &                                      Bulk, N_Phi_vec, N_FL, List, Invlist, Latt, Latt_unit )
          Call  Predefined_Hoppings_set_OPT(Hopping_Matrix,List,Invlist,Latt,  Latt_unit,  Dtau, Checkerboard, Symm, OP_T )
          
          Deallocate (Ham_T_vec, Ham_T2_vec, Ham_Tperp_vec, Ham_Chem_vec, Phi_X_vec, Phi_Y_vec, &
               &                                   N_Phi_vec,  Ham_Lambda_vec )


        end Subroutine Ham_Hop
!--------------------------------------------------------------------
!> @author 
!> ALF Collaboration
!>
!> @brief
!> Sets the interaction
!--------------------------------------------------------------------
        Subroutine Ham_V

          Use Predefined_Int
          Implicit none 
        
          Integer :: nf, I, I1, I2,  nc, nc1,  J, N_Field_type, N_ops
          Real (Kind=Kind(0.d0)) :: X

          N_ops = size(Field_list_inv,1)
          Allocate(Op_V(N_ops, N_FL))


          !Field_list_inv(nc,1) = I
          !Field_list_inv(nc,2) = n_orientation
          !Field_list_inv(nc,3) = N_Field_type
          Do nc = 1, N_ops
             N_Field_type = Field_list_inv(nc,3)
             select case (N_Field_type)
             case (3 ) ! Hubbard
                I = Field_list_inv(nc,1) 
                do nf = 1,N_FL
                   Call Predefined_Int_U_SUN( OP_V(nc,nf), I, N_SUN, DTAU, Ham_U  )
                enddo
             case (1 ) ! Z2_Gauge
                I = Field_list_inv(nc,1)
                select case ( Field_list_inv(nc,2) )
                case (1) 
                   I1 = Latt%nnlist(I,1,0)
                case (2)
                   I1 = Latt%nnlist(I,0,1)
                end select
                do nf = 1,N_FL
                   Call Predefined_Int_Ising_SUN( OP_V(nc,nf), I, I1, DTAU, -Ham_TZ2  )
                enddo
             case (2 ) ! Bond_Matter
                I = Field_list_inv(nc,1)
                select case ( Field_list_inv(nc,2) )
                case (1) 
                   I1 = Latt%nnlist(I,1,0)
                case (2)
                   I1 = Latt%nnlist(I,0,1)
                end select
                do nf = 1,N_FL
                   Call Predefined_Int_Ising_SUN( OP_V(nc,nf), I, I1, DTAU, -Ham_T  )
                enddo
             case (4 ) ! Site Matter
                I = Field_list_inv(nc,1)
                do nf = 1,N_FL
                   Call OP_Make( Op_V(nc,nf),1)
                   Op_V(nc,nf)%P(1)   = I1
                   Op_V(nc,nf)%O(1,1) = cmplx(1.d0,0.d0, kind(0.D0))
                   Op_V(nc,nf)%g      = cmplx(0.d0,0.d0, kind(0.D0))
                   Op_V(nc,nf)%alpha  = cmplx(0.d0,0.d0, kind(0.D0))
                   Op_V(nc,nf)%type   = 1
                   Call Op_set( Op_V(nc,1) )
                enddo
             end select
          end Do

        end Subroutine Ham_V

!--------------------------------------------------------------------
!> @author 
!> ALF Collaboration
!>
!> @brief
!> Single spin flip S0 ratio
!> @details
!> S0=exp(-S0(new))/exp(-S0(old)) where the new configuration correpsonds to the old one up to
!> a spin flip of Operator n on time slice nt
!> @details
!--------------------------------------------------------------------
        Real (Kind=Kind(0.d0)) function S0(n,nt,Hs_new)
          Implicit none
          Integer, Intent(IN) :: n,nt
          Real (Kind=Kind(0.d0)), Intent(In) :: Hs_new

          !Local
          Integer :: nt1,I, F1,F2,I1,I2,I3,  n_orientation, n_m

          !> Ratio for local spin-flip  of gauge field only.
          S0 = 1.d0

          If ( Abs(Ham_TZ2) > Zero ) then

             !Field_list_inv(nc,1) = I1
             !Field_list_inv(nc,2) = n_orientation
             !Field_list_inv(nc,3) = N_Field_type

             If (Field_list_inv(n,3) == 1 ) then

                If (Abs(Ham_T) > Zero ) then
                   I              = Field_list_inv(n,1)
                   n_orientation  = Field_list_inv(n,2)
                   n_m            = Field_list(I,n_orientation,2)
                   S0 = S0* DW_Ising_Matter(nsigma%i(n,nt)*nsigma%i(n_m,nt) )  ! Coupling to matter field.
                endif
                
                nt1 = nt +1
                if (nt1 > Ltrot) nt1 = 1
                S0 = S0*DW_Ising_tau(nsigma%i(n,nt)*nsigma%i(n,nt1))
                nt1 = nt - 1
                if (nt1 < 1  ) nt1 = Ltrot
                S0 = S0*DW_Ising_tau(nsigma%i(n,nt)*nsigma%i(n,nt1))
                ! Magnetic flux term
                I1 = Field_list_inv(n,1)
                if ( Field_list_inv(n,2) == 1 ) then
                   !     I2
                   !     I1 I3
                   I2 = Latt%nnlist(I1,0,1 )
                   I3 = Latt%nnlist(I1,1,0 )
                   F1 = nsigma%i(n,nt)*nsigma%i(Field_list(I1,2,1),nt)* nsigma%i(Field_list(I2,1,1),nt)*nsigma%i(Field_list(I3,2,1),nt)
                   !     I1
                   !     I2 I3
                   I2 = Latt%nnlist(I1,0,-1)
                   I3 = Latt%nnlist(I1,1,-1)
                   F2 = nsigma%i(n,nt)*nsigma%i(Field_list(I2,1,1),nt)* nsigma%i(Field_list(I2,2,1),nt)*nsigma%i(Field_list(I3,2,1),nt)
                else
                   !    I3
                   !    I2  I1
                   I2 = Latt%nnlist(I1,-1,0 )
                   I3 = Latt%nnlist(I1,-1,1 )
                   F1 = nsigma%i(n,nt)*nsigma%i(Field_list(I2,1,1),nt)* nsigma%i(Field_list(I2,2,1),nt)*nsigma%i(Field_list(I3,1,1),nt)
                   !    I2
                   !    I1  I3
                   I2 = Latt%nnlist(I1,0,1)
                   I3 = Latt%nnlist(I1,1,0)
                   F2 = nsigma%i(n,nt)*nsigma%i(Field_list(I1,1,1),nt)* nsigma%i(Field_list(I2,1,1),nt)*nsigma%i(Field_list(I3,2,1),nt)
                endif
                S0 = S0*DW_Ising_Flux(F1,F2)
             else
                Write(error_unit,*) 'Error in funtion S0'
                error stop 1
             endif

          endif

          
        end function S0

!===================================================================================
        Subroutine Global_move_tau(T0_Proposal_ratio, S0_ratio, &
             &                     Flip_list, Flip_length,Flip_value,ntau)

!--------------------------------------------------------------------
!> @author
!> ALF Collaboration
!>
!> @brief
!> On input:
!> GR(tau,m) as defined in  Global_tau_mod_PlaceGR and the direction of updating scheme
!> direction=u --> You are visiting the time slices from tau = 1  to tau =Ltrot
!> direction=d --> You are visiting the time slices from tau = Ltrot to tau = 1
!>
!> On input the field configuration is in the array nsigma.
!> On output:
!> Flip_list   ::  A list of spins that are to be fliped. Refers to the entires  in OP_V
!> Flip_values ::  The values of the fliped spins
!> Flip_length ::  The number of flips. The first Flip_length entries of Flip_list and Flip_values are relevant
!> S0_ratio          = e^( S_0(sigma_new) ) / e^( S_0(sigma) )
!> T0_Proposal_ratio = T0( sigma_new -> sigma ) /  T0( sigma -> sigma_new)
!> The move will be carried out with prbablity  T0 ( sigma -> sigma_new ).   If T0 ( sigma -> sigma_new ) > Ranf
!>  then T0_Proposal_ratio  will be initialized. Otherwise the latter quantity is set to zero.
!--------------------------------------------------------------------

          Implicit none
          Real (Kind= kind(0.d0)),INTENT(OUT) :: T0_Proposal_ratio, S0_ratio
          Integer                ,INTENT(OUT) :: Flip_list(:)
          Real (Kind= Kind(0.d0)),INTENT(out) :: Flip_value(:)
          Integer, INTENT(OUT) :: Flip_length
          Integer, INTENT(IN)    :: ntau


          !Local
          Integer                   ::  ns , nc, n_op, n_op1, ntau_p1, ntau_m1, I, n
          Integer, allocatable      ::  Isigma1(:),Isigma2(:),Isigma3(:)
          Real  (Kind = Kind(0.d0)) ::  S0_Matter, T0_Proposal

          ! Write(6,*) 'In GLob_move', m,direction,ntau, size(Flip_list,1), Size(Flip_value,1), Flip_list(1)
          ! Ising from n_op = 1,Latt_unit%N_coord*Ndim
          ! Hubbard from n_op = Latt_unit%N_coord*Ndim +1, Size(OP_V,1) = Latt_unit%N_coord*Ndim +  Ndim
          ! Write(6,*) 'Global_move_tau ' , S0(Flip_list(1),ntau)

          Allocate (Isigma1(Latt%N), Isigma2(Latt%N), Isigma3(Latt%N) )

          I  =  nranf(Latt%N)
          Flip_length = 4
          S0_Matter = 1.d0
          do n = 1,4
             select case(n)
             case (1)
                n_op  = Field_list(I,1,2)
                if ( Abs(Ham_TZ2) > Zero) then
                   n_op1 = Field_list(I,1,1)
                   S0_Matter = S0_Matter*DW_Ising_Matter( nsigma%i(n_op,ntau) *  nsigma%i(n_op1,ntau) )
                endif
             case (2)
                n_op  = Field_list(I,2,2)
                if ( Abs(Ham_TZ2) > Zero) then
                   n_op1 = Field_list(I,2,1)
                   S0_Matter = S0_Matter*DW_Ising_Matter( nsigma%i(n_op,ntau) *  nsigma%i(n_op1,ntau) )
                endif
             case (3)
                n_op  = Field_list(latt%nnlist(I,-1,0),1,2)
                if ( Abs(Ham_TZ2) > Zero) then
                   n_op1 = Field_list(latt%nnlist(I,-1,0),1,1)
                   S0_Matter = S0_Matter*DW_Ising_Matter( nsigma%i(n_op,ntau) *  nsigma%i(n_op1,ntau) )
                endif
             case (4)
                n_op  = Field_list(latt%nnlist(I,0,-1),2,2)
                if ( Abs(Ham_TZ2) > Zero) then
                   n_op1 = Field_list(latt%nnlist(I,0,-1),2,1)
                   S0_Matter = S0_Matter*DW_Ising_Matter( nsigma%i(n_op,ntau) *  nsigma%i(n_op1,ntau) )
                endif
             case default
                Write(error_unit,*) 'Global_move_tau: Error'
                error stop 1
             end select
             Flip_list(n)  = n_op
             Flip_value(n) = nsigma%flip(n_op,ntau)
          enddo
          If ( I == Latt%N )   then
             Flip_length   = 5
             n             = 5
             n_op          = Field_list(Latt%N,3,4)
             Flip_list(n)  = n_op
             Flip_value(n) = nsigma%flip(n_op,ntau)
          endif

          ntau_p1 = ntau + 1
          if (ntau == Ltrot) ntau_p1 = 1
          ntau_m1 = ntau -1
          if (ntau == 1    ) ntau_m1 = Ltrot
          Call Hamiltonian_set_Z2_matter(Isigma1,ntau_p1)
          Call Hamiltonian_set_Z2_matter(Isigma2,ntau   )
          Call Hamiltonian_set_Z2_matter(Isigma3,ntau_m1)
          !  Check the dynamics and the ergodicity
          S0_Matter = S0_Matter*DW_Matter_tau ( Isigma1(I)*Isigma2(I) ) * DW_Matter_tau( Isigma2(I)*Isigma3(I) )
          T0_Proposal       =  1.d0 - 1.d0/(1.d0+S0_Matter)
          !  Move acceptance probability.
          If ( T0_Proposal > Ranf_wrap() )  then
             T0_Proposal_ratio =  1.d0 / S0_Matter
             !T0_Proposal       =  1.d0
             !T0_Proposal_ratio =  1.d0
          else
             T0_Proposal_ratio = 0.d0
          endif
          S0_ratio          =  S0_Matter

          Deallocate (Isigma1,Isigma2, Isigma3)
!!$          Flip_length    = 1
!!$          n_op = nranf(size(OP_V,1))
!!$          Flip_list(1)   = n_op
!!$          If ( OP_V(n_op,1)%type == 1 ) then
!!$             ns = nsigma(n_op,ntau)
!!$             T0_Proposal       =  1.d0 - 1.d0/(1.d0+S0(n_op,ntau)) ! No move prob
!!$             T0_Proposal_ratio =  1.d0 / S0(n_op,ntau)
!!$             S0_ratio          =  S0(n_op,ntau)
!!$             Flip_value(1)     = - ns
!!$          else
!!$             Flip_value(1)     = NFLIPL(nsigma(n_op,ntau),nranf(3))
!!$             T0_Proposal       = 1.d0
!!$             T0_Proposal_ratio = 1.d0
!!$             S0_ratio          = 1.d0
!!$          endif

        end Subroutine Global_move_tau
!===================================================================================
        Subroutine Global_move(T0_Proposal_ratio,nsigma_old,size_clust)
          !>  The input is the field nsigma declared in this module. This routine generates a
          !>  global update with  and returns the propability
          !>  T0_Proposal_ratio  =  T0( sigma_out-> sigma_in ) /  T0( sigma_in -> sigma_out)
          !>

          Implicit none
          Real (Kind=Kind(0.d0)), intent(out) :: T0_Proposal_ratio, size_clust
          type (Fields),  Intent(IN)  :: nsigma_old
          !> nsigma_old contains a copy of nsigma upon entry


        End Subroutine Global_move
!===================================================================================
        Real (Kind=kind(0.d0)) Function Delta_S0_global(Nsigma_old)

          !>  This function computes the ratio:  e^{-S0(nsigma)}/e^{-S0(nsigma_old)}
          Implicit none

          !> Arguments
          type (Fields),  Intent(IN)  :: nsigma_old
          !> Local
          Integer :: I,n,n1,n2,n3,n4,nt,nt1, nc_F, nc_J, nc_h_p, nc_h_m, n1_m, n4_m


          Delta_S0_global = 1.d0
          If ( Model == "Z2_Matter" ) then
             nc_F = 0
             nc_J = 0
             nc_h_p = 0
             nc_h_m = 0
             Do I = 1,Latt%N
                n1   = Field_list(I,1,1)
                n1_m = Field_list(I,1,2)
                n2   = Field_list(Latt%nnlist(I,1,0),2,1)
                n3   = Field_list(Latt%nnlist(I,0,1),1,1)
                n4   = Field_list(I,2,1)
                n4_m = Field_list(I,2,2)
                do nt = 1,Ltrot
                   nt1 = nt +1
                   if (nt == Ltrot) nt1 = 1
                   if (nsigma%i(n1,nt) == nsigma%i(n1,nt1) ) then
                      nc_h_p = nc_h_p + 1
                   else
                      nc_h_m = nc_h_m + 1
                   endif
                   if (nsigma_old%i(n1,nt) == nsigma_old%i(n1,nt1) ) then
                      nc_h_p = nc_h_p - 1
                   else
                      nc_h_m = nc_h_m - 1
                   endif

                   if (nsigma%i(n4,nt) == nsigma%i(n4,nt1) ) then
                      nc_h_p = nc_h_p + 1
                   else
                      nc_h_m = nc_h_m + 1
                   endif
                   if (nsigma_old%i(n4,nt) == nsigma_old%i(n4,nt1) ) then
                      nc_h_p = nc_h_p - 1
                   else
                      nc_h_m = nc_h_m - 1
                   endif

                   nc_F = nc_F + nsigma%i    (n1,nt)*nsigma%i    (n2,nt)*nsigma%i    (n3,nt)*nsigma%i    (n4,nt)  &
                        &      - nsigma_old%i(n1,nt)*nsigma_old%i(n2,nt)*nsigma_old%i(n3,nt)*nsigma_old%i(n4,nt)

                   nc_J = nc_J + nsigma%i(n1,nt)*nsigma%i(n1_m,nt) + &
                        &        nsigma%i(n4,nt)*nsigma%i(n4_m,nt) - &
                        &        nsigma_old%i(n1,nt)*nsigma_old%i(n1_m,nt) - &
                        &        nsigma_old%i(n4,nt)*nsigma_old%i(n4_m,nt)

                enddo
             enddo
             Delta_S0_global = ( sinh(Dtau*Ham_h)**nc_h_m ) * (cosh(Dtau*Ham_h)**nc_h_p) * &
                  &            exp( -Dtau*(Ham_K*real(nc_F,kind(0.d0)) + Ham_J*real(nc_J,kind(0.d0))))
          endif
        end Function Delta_S0_global

!--------------------------------------------------------------------
!> @author
!> ALF Collaboration
!>
!> @brief
!> This routine sets storage to estimate Ising action as well as the list and types of fields (HS or Ising) 
!> so as to know if the field nsimg%i(nc,nt) corresponds to a HS field for the U term, an Ising gauge field,
!> or a Z_2 matter field.
!>  Field_list_inv(nc,1) = I              ! Postion on lattice 
!>  Field_list_inv(nc,2) = n_orientation  ! Orientation 1=a_x, 2 = a_y , 3 = no-orientation for on-site interaction. 
!>  Field_list_inv(nc,3) = Field_type     ! 1 = gauge Field, 2 = Bond matter field, 3 =  HS for Hubbard, 4 = Matter field at a given site. 

!--------------------------------------------------------------------
        Subroutine Setup_Ising_action_and_field_list

          ! This subroutine sets up lists and arrays so as to enable an
          ! an efficient calculation of  S0(n,nt)

          Integer :: nc, nth, n, n1, n2, n3, n4, I, I1, n_orientation, Ix, Iy, N_Field_type,  N_Pos
          Integer :: N_ops
          Real (Kind=Kind(0.d0)) :: X_p(2)


          N_ops = 0
          If (Abs(Ham_U)   > Zero )   N_ops = N_ops + Latt%N                          !  Hubbard
          If (Abs(Ham_TZ2) > Zero )   N_ops = N_ops + Latt%N*Latt_unit%N_coord        !  Z2 gauge fields
          If (Abs(Ham_T  ) > Zero )   N_ops = N_ops + Latt%N*Latt_unit%N_coord + 1    !  Matter fields.
          
          ! Setup list of bonds for the square lattice.
          Allocate ( Field_list(Latt%N,3,4),  Field_list_inv(N_ops,3) )
          nc = 0
          If (Abs(Ham_U)   > Zero )  then
             DO I = 1,Latt%N
                nc = nc + 1
                N_Pos         = I
                N_orientation = 3
                N_Field_type  = 3
                Field_list_inv(nc,1) = N_Pos
                Field_list_inv(nc,2) = N_Orientation
                Field_list_inv(nc,3) = N_Field_type
                Field_list(N_pos,n_orientation,N_field_type) = nc
             Enddo
          Endif
          If (Abs(Ham_TZ2)   > Zero )  then
             N_Field_type = 1
             DO I = 1,Latt%N
                Ix = Latt%list(I,1)
                Iy = Latt%list(I,2)
                if (mod(Ix + Iy,2) == 0 ) then
                   do n = 1,4
                      nc = nc + 1
                      select case (n)
                      case (1)
                         I1 = I                  ;  n_orientation  = 1
                      case (2)
                         I1 = I                  ;  n_orientation  = 2
                      case (3)
                         I1 = latt%nnlist(I,-1,0);  n_orientation  = 1
                      case (4)
                         I1 = latt%nnlist(I,0,-1);  n_orientation  = 2
                      case default
                         Write(6,*) ' Error in Setup_Ising_action '
                      end select
                      Field_list(I1,n_orientation,N_Field_type) = nc
                      Field_list_inv(nc,1) = I1
                      Field_list_inv(nc,2) = n_orientation
                      Field_list_inv(nc,3) = N_Field_type
                      ! The bond is given by  I1, I1 + a_(n_orientation).
                   enddo
                endif
             Enddo
          Endif
          If (Abs(Ham_T)   > Zero )  then
             N_Field_type = 2
             DO I = 1,Latt%N
                Ix = Latt%list(I,1)
                Iy = Latt%list(I,2)
                if (mod(Ix + Iy,2) == 0 ) then
                   do n = 1,4
                      nc = nc + 1
                      select case (n)
                      case (1)
                         I1 = I                  ;  n_orientation  = 1
                      case (2)
                         I1 = I                  ;  n_orientation  = 2
                      case (3)
                         I1 = latt%nnlist(I,-1,0);  n_orientation  = 1
                      case (4)
                         I1 = latt%nnlist(I,0,-1);  n_orientation  = 2
                      case default
                         Write(6,*) ' Error in Setup_Ising_action '
                      end select
                      Field_list(I1,n_orientation,N_Field_type) = nc
                      Field_list_inv(nc,1) = I1
                      Field_list_inv(nc,2) = n_orientation
                      Field_list_inv(nc,3) = N_Field_type
                      ! The bond is given by  I1, I1 + a_(n_orientation).
                   enddo
                endif
             Enddo
             nc = nc + 1
             I = Latt%N
             n_orientation = 3
             N_Field_type  = 4
             Field_list(I,n_orientation,N_Field_type) = nc
             Field_list_inv(nc,1) = I
             Field_list_inv(nc,2) = n_orientation
             Field_list_inv(nc,3) = N_Field_type
          Endif

          !Test
          !Do I = 1,Latt%N
          !   Write(6,*)
          !   Write(6,*) Latt%list(I,1), Latt%list(I,2), I
          !   Write(6,*) Field_list(I,1), Field_list(I,2), Field_list( latt%nnlist(I,-1,0),1), Field_list( latt%nnlist(I,0,-1),2)
          !Enddo

          DW_Ising_tau = 1.d0
          If (Ham_g > Zero ) then
             DW_Ising_tau  ( 1) = tanh(Dtau*Ham_g)
             DW_Ising_tau  (-1) = 1.D0/DW_Ising_tau(1)
          endif
          DO n = -1,1,2
             do n1 = -1,1,2
                DW_Ising_Flux(n,n1) = exp( Dtau*Ham_K*(dble(n) +  dble(n1) ))/ exp(  -Dtau*Ham_K*(dble(n) +  dble(n1) ))
             enddo
          enddo
          DW_Ising_Matter( 1) = exp( 2.d0*Dtau*Ham_J)
          DW_Ising_Matter(-1) = exp(-2.d0*Dtau*Ham_J)
          DW_Matter_tau = 1.d0
          If (Ham_h > Zero ) then
             DW_Matter_tau  ( 1) = tanh(Dtau*Ham_h)
             DW_Matter_tau  (-1) = 1.D0/DW_Matter_tau(1)
          endif

        End Subroutine Setup_Ising_action_and_field_list
!===================================================================================
        Subroutine  Alloc_obs(Ltau)

          Implicit none
          Integer, Intent(In) :: Ltau
          Integer    ::  i, N, Ns,Nt,No
          Character (len=64) ::  Filename

          ! Scalar observables
          Allocate ( Obs_scal(3) )
          Do I = 1,Size(Obs_scal,1)
             select case (I)
             case (1)
                N = 1;   Filename ="Part"
             case (2)
                N = 2;   Filename ="Flux"
             case (3)
                N = 2;   Filename ="X"
             case default
                Write(6,*) ' Error in Alloc_obs '
             end select
             Call Obser_Vec_make(Obs_scal(I),N,Filename)
          enddo


          ! Equal time correlators
          Allocate ( Obs_eq(5) )
          Do I = 1,Size(Obs_eq,1)
             select case (I)
             case (1)
                Ns = Latt%N;  No = Latt_unit%Norb;  Filename ="Green"
             case (2)
                Ns = Latt%N;  No = Latt_unit%Norb;  Filename ="SpinZ"
             case (3)
                Ns = Latt%N;  No = Latt_unit%Norb;  Filename ="Den"
             case (4)
                Ns = Latt%N;  No = Latt_unit%Norb;  Filename ="GreenZ2"
             case (5)
                Ns = Latt%N;  No = Latt_unit%Norb;  Filename ="Q"
             case default
                Write(6,*) ' Error in Alloc_obs '
             end select
             Nt = 1
             Call Obser_Latt_make(Obs_eq(I),Ns,Nt,No,Filename)
          enddo

          If (Ltau == 1) then
             ! Equal time correlators
             Allocate ( Obs_tau(4) )
             Do I = 1,Size(Obs_tau,1)
                select case (I)
                case (1)
                   Ns = Latt%N; No = Latt_unit%Norb;  Filename ="Green"
                case (2)
                   Ns = Latt%N; No = Latt_unit%Norb;  Filename ="SpinZ"
                case (3)
                   Ns = Latt%N; No = Latt_unit%Norb;  Filename ="SpinXY"
                case (4)
                   Ns = Latt%N; No = Latt_unit%Norb;  Filename ="Den"
                case default
                   Write(6,*) ' Error in Alloc_obs '
                end select
                Nt = Ltrot+1
                Call Obser_Latt_make(Obs_tau(I),Ns,Nt,No,Filename)
             enddo
          endif

        end Subroutine Alloc_obs

!========================================================================
        Subroutine Obser(GR,Phase,Ntau)


          Implicit none

          Complex (Kind=Kind(0.d0)), INTENT(IN) :: GR(Ndim,Ndim,N_FL)
          Complex (Kind=Kind(0.d0)), Intent(IN) :: PHASE
          Integer, INTENT(IN)          :: Ntau

          !Local
          Complex (Kind=Kind(0.d0)) :: GRC(Ndim,Ndim,N_FL), ZK
          Complex (Kind=Kind(0.d0)) :: Zrho, Zkin_mat, ZPot_mat, Z, ZP,ZS, Z1, Z2, ZN
          Complex (Kind=Kind(0.d0)) :: ZQ, ZSTAR, ZQT, ZQTT
          Integer :: I,J, imj, nf, dec, I1, I2,I3,I4, J1, no_I, no_J,  iFlux_tot,  &
               &     no, no1, ntau1, L_Vison, L_Wilson, n, nx,ny
          Real (Kind=Kind(0.d0)) :: X_ave, X, XI1,XI2,XI3,XI4
          Integer,  allocatable  :: Isigma(:), Isigma1(:)
          Integer ::  IB_x, IB_y, Ix, Iy


          ZP = PHASE/Real(Phase, kind(0.D0))
          ZS = Real(Phase, kind(0.D0))/Abs(Real(Phase, kind(0.D0)))

          ZN =  cmplx(dble(N_SUN), 0.d0, kind(0.D0))

          Do nf = 1,N_FL
             Do I = 1,Ndim
                Do J = 1,Ndim
                   GRC(I, J, nf) = -GR(J, I, nf)
                Enddo
                GRC(I, I, nf) = 1.D0 + GRC(I, I, nf)
             Enddo
          Enddo
          ! GRC(i,j,nf) = < c^{dagger}_{j,nf } c_{j,nf } >

          ! Compute scalar observables.
          Do I = 1,Size(Obs_scal,1)
             Obs_scal(I)%N         =  Obs_scal(I)%N + 1
             Obs_scal(I)%Ave_sign  =  Obs_scal(I)%Ave_sign + Real(ZS,kind(0.d0))
          Enddo

          If ( abs(Ham_T) > Zero ) then
             ntau1 = ntau + 1
             If (ntau == Ltrot)  ntau1 = 1
             Allocate ( Isigma(Latt%N), Isigma1(Latt%N) )
             Call Hamiltonian_set_Z2_matter(Isigma ,ntau )
             Call Hamiltonian_set_Z2_matter(Isigma1,ntau1)
             
             iFlux_tot = 0
             Do I = 1, Ndim
                iFlux_tot = iFlux_tot + iFlux(I,Ntau,2)
             Enddo
             Obs_scal(2)%Obs_vec(2)  =   Obs_scal(2)%Obs_vec(2) + cmplx(dble(iFlux_tot),0.d0,kind(0.d0))*ZP*ZS

             X_ave = 0.d0
             Do I = 1,Latt%N
                X_ave = X_ave + DW_Matter_tau( Isigma(I)*Isigma1(I) )
             Enddo
             Obs_scal(3)%Obs_vec(2)  =  Obs_scal(3)%Obs_vec(2) + cmplx(X_ave,0.d0,kind(0.d0)) * ZP*ZS

          endif

          Zrho = cmplx(0.d0,0.d0, kind(0.D0))
          Do nf = 1,N_FL
             Do I = 1,Latt%N
                Zrho = Zrho + Grc(i,i,nf)
             enddo
          enddo
          Zrho = Zrho* dble(N_SUN)
          Obs_scal(1)%Obs_vec(1)  =    Obs_scal(1)%Obs_vec(1) + Zrho * ZP*ZS

          
          If ( abs(Ham_TZ2) > Zero ) then
             iFlux_tot = 0
             Do I = 1, Ndim
                iFlux_tot = iFlux_tot + iFlux(I,Ntau,1)
             Enddo
             Obs_scal(2)%Obs_vec(1)  =   Obs_scal(2)%Obs_vec(1) + cmplx(dble(iFlux_tot),0.d0,kind(0.d0))*ZP*ZS
             
             ntau1 = ntau + 1
             If (ntau == Ltrot)  ntau1 = 1
             X_ave = 0.d0
             Do I = 1,Latt%N
                do no = 1,2
                   X_ave = X_ave + DW_Ising_tau( nsigma%i(Field_list(I,no,1),ntau)*nsigma%i(Field_list(I,no,1),ntau1) )
                Enddo
             Enddo
             Obs_scal(3)%Obs_vec(1)  =  Obs_scal(3)%Obs_vec(1) + cmplx(X_ave,0.d0,kind(0.d0)) * ZP*ZS
             
          Endif
             

             

          ! Compute spin-spin, Green, and den-den correlation functions  !  This is general N_SUN, and  N_FL = 1
          
          DO I = 1,Size(Obs_eq,1)
             Obs_eq(I)%N        = Obs_eq(I)%N + 1
             Obs_eq(I)%Ave_sign = Obs_eq(I)%Ave_sign + real(ZS,kind(0.d0))
          ENDDO

          If ( abs(Ham_T) > Zero ) then
             Z =  cmplx(dble(N_SUN), 0.d0, kind(0.D0))
             Do I1 = 1,Latt%N
                Do J1 = 1,Latt%N
                   imj = latt%imj(I1,J1)
                   ! Green_fermion
                   Z1 = cmplx(real(Isigma(I1)*Isigma(J1), kind(0.d0)), 0.d0,kind(0.d0))
                   Obs_eq(1)%Obs_Latt(imj,1,1,1) =  Obs_eq(1)%Obs_Latt(imj,1,1,1) + &
                        &               Z * Z1*GRC(I1,J1,1) *  ZP*ZS
                enddo
             enddo
          endif

          Z =  cmplx(dble(N_SUN), 0.d0, kind(0.D0))
          Do I1 = 1,Latt%N
             Do J1 = 1,Latt%N
                imj = latt%imj(I1,J1)
                ! SpinZ
                Obs_eq(2)%Obs_Latt(imj,1,1,1) =  Obs_eq(2)%Obs_Latt(imj,1,1,1) + &
                     &               Z * GRC(I1,J1,1) * GR(I1,J1,1) * ZP*ZS

                ! Den
                Obs_eq(3)%Obs_Latt(imj,1,1,1) =  Obs_eq(3)%Obs_Latt(imj,1,1,1)  +  &
                     &     (    GRC(I1,I1,1) * GRC(J1,J1,1) *Z     + &
                     &          GRC(I1,J1,1) * GR(I1,J1,1 )          &
                     &                                     ) * Z* ZP*ZS

                ! Green_Z2
                Obs_eq(4)%Obs_Latt(imj,1,1,1) =  Obs_eq(4)%Obs_Latt(imj,1,1,1) + &
                     &               Z * GRC(I1,J1,1) *  ZP*ZS

             enddo
             Obs_eq(3)%Obs_Latt0(1) =  Obs_eq(3)%Obs_Latt0(1) +  Z * GRC(I1,I1,1) * ZP * ZS
          ENDDO


          !  Constraint   
          If (Abs(Ham_TZ2) < Zero  .and. Abs(Ham_T) > Zero ) Then
             Do I1 = 1,Latt%N
                Do J1 = 1,Latt%N
                   imj = latt%imj(I1,J1)
                   Z1 =   (cmplx(1.d0,0.d0,kind(0.d0)) - cmplx(2.d0,0.d0,kind(0.d0))*GRC(I1,I1,1)) *  &
                        & (cmplx(1.d0,0.d0,kind(0.d0)) - cmplx(2.d0,0.d0,kind(0.d0))*GRC(J1,J1,1)) +  &
                        &  cmplx(4.d0,0.d0,kind(0.d0)) * GRC(I1,J1,1)*GR(I1,J1,1)
                   Z1 = Z1**(N_SUN)
                   ZQ = cmplx(DW_Matter_tau( Isigma(I1)*Isigma1(I1))*DW_Matter_tau( Isigma(J1)*Isigma1(J1)),0.d0,kind(0.d0) )*Z1
                   If ( I1 == J1 .and.  mod(N_SUN,2) == 0  )  ZQ = cmplx(1.d0,0.d0,kind(0.d0))
                   If ( I1 == J1 .and.  mod(N_SUN,2) == 1  )  then
                       Z1 = cmplx(1.d0,0.d0,kind(0.d0)) - cmplx(2.d0,0.d0,kind(0.d0))*GRC(I1,I1,1)
                       Z1 = Z1**(N_SUN)
                       ZQ = cmplx(DW_Matter_tau( Isigma(I1)*Isigma1(I1)),0.d0,kind(0.d0) )*Z1
                   endif
                   Obs_eq(5)%Obs_Latt(imj,1,1,1) =  Obs_eq(5)%Obs_Latt(imj,1,1,1) + ZQ*ZP*ZS
                Enddo
                Z1 = cmplx(1.d0,0.d0,kind(0.d0)) - cmplx(2.d0,0.d0,kind(0.d0))*GRC(I1,I1,1)
                Z1 = Z1**(N_SUN)
                ZQ = cmplx(DW_Matter_tau( Isigma(I1)*Isigma1(I1)),0.d0,kind(0.d0) )*Z1
                Obs_eq(5)%Obs_Latt0(1)  = Obs_eq(5)%Obs_Latt0(1) + ZQ*ZP*ZS
             Enddo
          elseif (Abs(Ham_TZ2) > Zero  .and. Abs(Ham_T) < Zero ) Then
          else
          endif

          
          If (Abs(Ham_T) > Zero ) Deallocate ( Isigma, Isigma1 )

        end Subroutine Obser
!--------------------------------------------------------------------
!> @author 
!> ALF Collaboration
!>
!> @brief 
!> Computes time displaced  observables
!> @details
!> @param [IN] NT, Integer
!> \verbatim
!>  Imaginary time
!> \endverbatim
!> @param [IN] GT0, GTT, G00, GTT,  Complex(:,:,:)  
!> \verbatim
!>  Green functions:
!>  GT0(I,J,nf) = <T c_{I,nf }(tau) c^{dagger}_{J,nf }(0  )> 
!>  G0T(I,J,nf) = <T c_{I,nf }(0  ) c^{dagger}_{J,nf }(tau)> 
!>  G00(I,J,nf) = <T c_{I,nf }(0  ) c^{dagger}_{J,nf }(0  )> 
!>  GTT(I,J,nf) = <T c_{I,nf }(tau) c^{dagger}_{J,nf }(tau)> 
!> \endverbatim
!> @param [IN] Phase   Complex
!> \verbatim
!>  Phase  
!> \endverbatim
!-------------------------------------------------------------------

        Subroutine ObserT(NT,  GT0,G0T,G00,GTT, PHASE)
          Implicit none

          Integer         , INTENT(IN) :: NT
          Complex (Kind=Kind(0.d0)), INTENT(IN) :: GT0(Ndim,Ndim,N_FL),G0T(Ndim,Ndim,N_FL),G00(Ndim,Ndim,N_FL),GTT(Ndim,Ndim,N_FL)
          Complex (Kind=Kind(0.d0)), INTENT(IN) :: Phase

          !Locals
          Complex (Kind=Kind(0.d0)) :: Z, ZP, ZS
          Integer :: IMJ, I, J, I1, J1, no_I, no_J, NT1

          NT1 = NT
          If (NT == 0 ) NT1 = LTROT
          ZP = PHASE/Real(Phase, kind(0.D0))
          ZS = Real(Phase, kind(0.D0))/Abs(Real(Phase, kind(0.D0)))
          If (NT == 0 ) then
             DO I = 1,Size(Obs_tau,1)
                Obs_tau(I)%N = Obs_tau(I)%N + 1
                Obs_tau(I)%Ave_sign = Obs_tau(I)%Ave_sign + Real(ZS,kind(0.d0))
             ENDDO
          endif
          If ( Model == "Z2_Matter" ) then
             Z =  cmplx(dble(N_SUN),0.d0, kind(0.D0))
             Do I1 = 1,Ndim
                I    = List(I1,1)
                no_I = List(I1,2)
                Do J1 = 1,Ndim
                   J    = List(J1,1)
                   no_J = List(J1,2)
                   imj = latt%imj(I,J)
                   ! Green
                   Obs_tau(1)%Obs_Latt(imj,nt+1,no_I,no_J) =  Obs_tau(1)%Obs_Latt(imj,nt+1,no_I,no_J)  &
                        & +  Z * GT0(I1,J1,1) * ZP* ZS

                   ! SpinZ
                   Obs_tau(2)%Obs_Latt(imj,nt+1,no_I,no_J) =  Obs_tau(2)%Obs_Latt(imj,nt+1,no_I,no_J)  &
                        &      - Z*G0T(J1,I1,1) * GT0(I1,J1,1) *ZP*ZS

                   ! SpinXY
                   Obs_tau(3)%Obs_Latt(imj,nt+1,no_I,no_J) =  Obs_tau(3)%Obs_Latt(imj,nt+1,no_I,no_J)  &
                        &      - Z*G0T(J1,I1,1) * GT0(I1,J1,1) *ZP*ZS

                   ! Den
                   Obs_tau(4)%Obs_Latt(imj,nt+1,no_I,no_J) =  Obs_tau(4)%Obs_Latt(imj,nt+1,no_I,no_J)  &
                        & + ( Z*Z*(cmplx(1.d0,0.d0,kind(0.d0)) - GTT(I1,I1,1))*       &
                        &         (cmplx(1.d0,0.d0,kind(0.d0)) - G00(J1,J1,1))  -     &
                        &     Z * GT0(I1,J1,1)*G0T(J1,I1,1)                                ) * ZP * ZS
                Enddo
                Obs_tau(4)%Obs_Latt0(no_I) = Obs_tau(4)%Obs_Latt0(no_I) + &
                     &         Z*(cmplx(1.d0,0.d0,kind(0.d0)) - GTT(I1,I1,1)) * ZP * ZS
             Enddo
          Endif

        end Subroutine OBSERT
!==========================================================
        Subroutine  Pr_obs(LTAU)

          Implicit none

          Integer,  Intent(In) ::  Ltau

          !Local
          Integer :: I


          Do I = 1,Size(Obs_scal,1)
             Call  Print_bin_Vec(Obs_scal(I),Group_Comm)
          enddo
          Do I = 1,Size(Obs_eq,1)
             Call  Print_bin_Latt(Obs_eq(I),Latt,dtau,Group_Comm)
          enddo
          If (Ltau  == 1 ) then
             Do I = 1,Size(Obs_tau,1)
                Call  Print_bin_Latt(Obs_tau(I),Latt,dtau,Group_Comm)
             enddo
          endif

        end Subroutine Pr_obs
!===================================================================================
        Subroutine  Init_obs(Ltau)

          Implicit none
          Integer, Intent(In) :: Ltau

          ! Local
          Integer :: I

          Do I = 1,Size(Obs_scal,1)
             Call Obser_vec_Init(Obs_scal(I))
          Enddo

          Do I = 1,Size(Obs_eq,1)
             Call Obser_Latt_Init(Obs_eq(I))
          Enddo

          If (Ltau == 1) then
             Do I = 1,Size(Obs_tau,1)
                Call Obser_Latt_Init(Obs_tau(I))
             Enddo
          Endif

        end Subroutine Init_obs

!===================================================================================

      Integer Function  iFlux(I,nt,nb_type)

        Implicit none

        Integer, INTENT(IN) :: I,nt, nb_type

        ! Local
        Integer :: n1,n2,n3,n4

        !   I3  I2
        !   I   I1
        n1  = Field_list(I,1,nb_type)
        n2  = Field_list(Latt%nnlist(I,1,0),2,nb_type)
        n3  = Field_list(Latt%nnlist(I,0,1),1,nb_type)
        n4  = Field_list(I,2,nb_type)
        iFlux =   nsigma%i(n1,nt)*nsigma%i(n2,nt)*nsigma%i(n3,nt)*nsigma%i(n4,nt)

      end Function iFlux

!===================================================================================
      Subroutine  Hamiltonian_set_nsigma(Initial_field)

        ! The user can set the initial configuration

        Implicit none

        Real (Kind=Kind(0.d0)), allocatable, dimension(:,:), Intent(OUT) :: Initial_field

        ! Local
        Integer :: I,nc, I1, nt, n_orientation, N_ops
        Integer, allocatable::  Isigma(:), Isigma1(:)
        Integer :: Iseed(1) 



        N_ops = size(Field_list_inv,1)

        Allocate  (Initial_field(N_ops, Ltrot) )
        allocate  (Isigma(Latt%N), Isigma1(Latt%N) )

        Initial_field = 0.d0
        If ( Abs(Ham_U) > Zero ) then
           do nt = 1,Ltrot
              do I = 1,Latt%N
                 nc = Field_list(I,3,3)
                 Initial_field(nc,nt) = 1.D0
                 if ( ranf_wrap()  > 0.5D0 ) Initial_field(nc,nt)  = -1.D0
              enddo
           enddo
        endif
        If ( Abs(Ham_TZ2) > Zero ) then
           !  Start with a pi-flux state.
           Do nt = 1,Ltrot
              Do I = 1, Latt%N
                 if (mod( Latt%list(i,1) + latt%list(i,2), 2 ) == 0 ) then
                    Initial_field(Field_list(I,1,1),nt) =  1.d0
                    Initial_field(Field_list(I,2,1),nt) = -1.d0
                 else
                    Initial_field(Field_list(I,1,1),nt) =  1.d0
                    Initial_field(Field_list(I,2,1),nt) =  1.d0
                 endif
              Enddo
           Enddo
        endif
        If ( Abs(Ham_T) > Zero ) then
           Do nt = 1,Ltrot
              Do I = 1,Latt%N
                 Isigma(I) = 1
                 if ( ranf_wrap()  > 0.5D0 ) Isigma(I)  = -1
              enddo
              Do I = 1,Latt%N
                 Do n_orientation = 1,2
                    nc = Field_list(I,n_orientation,2)
                    if (  n_orientation == 1 )  I1 = latt%nnlist(I,1,0)
                    if (  n_orientation == 2 )  I1 = latt%nnlist(I,0,1)
                    Initial_field(nc,nt) = real(Isigma(I)*Isigma(I1), kind(0.d0))
                 enddo
              Enddo
              Initial_field(Field_list(Latt%N,3,4),nt) = real(Isigma(Latt%N), kind(0.d0))
              do nc = 1,size(Initial_field,1)
                 nsigma%f(nc,nt) = Initial_field(nc,nt)
              enddo
              Call Hamiltonian_set_Z2_matter(Isigma1,nt)
              Do nc = 1,Latt%N
                 if ( Isigma(nc) .ne.  Isigma1(nc)  ) then
                    Write(error_unit,*) 'Error in Hamiltonian_set_Z2_matter'
                    error stop 1
                 endif
              enddo
           enddo
        endif

        deallocate (Isigma, Isigma1)

        

      end Subroutine Hamiltonian_set_nsigma
!--------------------------------------------------------------------    
!> @author 
!> ALF Collaboration
!>
!> @brief
!> Given the the HS fields nsigma  the  routine computes the site matter fields.
!> 
!> @details
!--------------------------------------------------------------------    
      Subroutine  Hamiltonian_set_Z2_matter(Isigma,nt)

        ! On input :  Link variables  nsigma(:,nt)
        ! On output:  The Z2_matter fields Isigma on the time slice.

        Implicit none

        Integer, Intent(IN)                  :: nt
        Integer, allocatable, INTENT(INOUT)  ::   Isigma(:)

        !Local
        Integer :: I, I1, nx, ny

        
        Isigma(Latt%N) = nsigma%i( Field_list(Latt%N,3,4), nt )
        I = Latt%N
        do nx = 1,L1
           do ny = 1,L2
              I1 = latt%nnlist(I,0,1)
              Isigma(I1)  = Isigma(I)*nsigma%i(Field_list(I,2,2),nt)
              !Write(6,*) Latt%list(I,1), Latt%list(I,2), ' -> ', Latt%list(I1,1), Latt%list(I1,2)
              I = I1
           enddo
           I1          = latt%nnlist(I,1,0)
           Isigma(I1)  = Isigma(I)*nsigma%i(Field_list(I,1,2),nt)
           !Write(6,*) Latt%list(I,1), Latt%list(I,2), ' -> ', Latt%list(I1,1), Latt%list(I1,2)
           I = I1
        enddo

      end Subroutine Hamiltonian_set_Z2_matter

!===================================================================================
      Subroutine Hamiltonian_Print(Ntau)

        Integer, Intent(IN) :: Ntau

        Integer, allocatable :: Isigma(:)
        Integer :: I, Ix, Iy

        allocate (Isigma(Latt%N))

        Call Hamiltonian_set_Z2_matter(Isigma,ntau)

        Write(6,*)'-----'
        I = 1
        Do Iy = 1,L2
           Do Ix = 1,L1
              Write(6,"(I2,1x)", advance='no')  Isigma(I)
              I = Latt%nnlist(I,1,0)
           enddo
           Write(6,*)
           I = Latt%nnlist(I,0,1)
        enddo

        deallocate (Isigma)
      End Subroutine Hamiltonian_Print
!!$!===================================================================================
!!$
!!$      Subroutine Print_fluxes
!!$
!!$
!!$#if defined (MPI) || defined(TEMPERING)
!!$        use  mpi
!!$#endif
!!$
!!$        Implicit none
!!$
!!$
!!$        ! Local
!!$        Integer :: I,nt,ix, iy, n
!!$        Character (len=64) :: File1
!!$
!!$
!!$#ifdef MPI
!!$        Integer        :: Isize, Irank, IERR, igroup, irank_g, isize_g
!!$        Integer        :: STATUS(MPI_STATUS_SIZE)
!!$        CALL MPI_COMM_SIZE(MPI_COMM_WORLD,ISIZE,IERR)
!!$        CALL MPI_COMM_RANK(MPI_COMM_WORLD,IRANK,IERR)
!!$        call MPI_Comm_rank(Group_Comm, irank_g, ierr)
!!$        call MPI_Comm_size(Group_Comm, isize_g, ierr)
!!$        igroup           = irank/isize_g
!!$#endif
!!$
!!$#if defined(TEMPERING)
!!$        write(File1,'(A,I0,A)') "Temp_",igroup,"/Fluxes"
!!$#else
!!$        File1="Fluxes"
!!$#endif
!!$
!!$        Open (Unit=10,File=File1, status="unknown")
!!$        Do nt = 1,Ltrot
!!$           Do i  = 1,Ndim
!!$              n = iFlux(I,nt,2)
!!$              if (n == -1 ) then
!!$                 ix = Latt%list(i,1)
!!$                 iy = Latt%list(i,2)
!!$                 Write(10,'(I4,2x,I4,2x,I4)')   IX, IY, NT
!!$              endif
!!$           Enddo
!!$        enddo
!!$        close(10)
!!$
!!$      end Subroutine Print_fluxes
!--------------------------------------------------------------------
!> @author
!> ALF Collaboration
!>
!> @brief
!> This routine allows to user to  determine the global_tau sampling parameters at run time
!> It is especially usefull if these parameters are dependent on other parameters.
!>
!> @details
!> \endverbatim
!--------------------------------------------------------------------
      Subroutine Overide_global_tau_sampling_parameters(Nt_sequential_start,Nt_sequential_end,N_Global_tau)

        Implicit none
        Integer, Intent(INOUT) :: Nt_sequential_start,Nt_sequential_end, N_Global_tau

        
        Nt_sequential_start = 1
        Nt_sequential_end   = 0
        If (abs(Ham_U  ) > Zero ) Nt_sequential_end = Nt_sequential_end + Latt%N
        If (abs(Ham_TZ2) > Zero ) Nt_sequential_end = Nt_sequential_end + Latt%N*Latt_unit%N_coord
        N_Global_tau = 0
        if (abs(Ham_T) > Zero )  N_Global_tau        = Latt%N/4

      end Subroutine Overide_global_tau_sampling_parameters
!===================================================================================
!!$      Subroutine Test_Hamiltonian
!!$
!!$        Implicit none
!!$
!!$        Integer :: n,  nc, n_op, nt
!!$        Integer, allocatable :: nsigma_old(:,:)
!!$        Real (Kind=kind(0.d0)) :: X, X1, size_clust
!!$
!!$        n = size(Op_V,1)
!!$        allocate (nsigma_old(n,Ltrot))
!!$        do nc = 1,100
!!$           !nt  = nranf(Ltrot)
!!$           !n_op= nranf(n)
!!$           !if ( OP_V(n_op,1)%type == 1 ) then
!!$           !   X = S0(n_op,nt)
!!$           !   nsigma_old = nsigma
!!$           !   nsigma(n_op,nt) = -nsigma(n_op,nt)
!!$           !   X1 = Delta_S0_global(Nsigma_old)
!!$           !   Write(6,*) nc, X, X1
!!$           !endif
!!$           nsigma_old = nsigma
!!$           Call Global_move(X,nsigma_old,size_clust)
!!$           X1 = Delta_S0_global(Nsigma_old)
!!$           Write(6,*) nc, X, X1
!!$        enddo
!!$        deallocate (nsigma_old)
!!$
!!$        stop
!!$
!!$      end Subroutine Test_Hamiltonian

      end Module Hamiltonian
